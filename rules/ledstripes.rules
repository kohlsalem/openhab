import org.openhab.core.library.types.*

var Timer timerGuteNacht = null  

rule "LEDStripesGuteNacht"
when
	Item LichtCouchtisch received update or
	Item LichtEsstisch received update
	
then
  logDebug("LEDStripes", "LEDStripesGuteNacht: Entering Rule")
 
  if( Sun_Elevation.state != null && 
      LichtCouchtisch.state != null &&
      LichtEsstisch.state != null
      ){   
    logDebug("LEDStripes", "LEDStripesGuteNacht: Sun_Elevation  : {}",Sun_Elevation.state )  	
    logDebug("LEDStripes", "LEDStripesGuteNacht: LichtCouchtisch: {}",LichtCouchtisch.state)  	
    logDebug("LEDStripes", "LEDStripesGuteNacht: LichtEsstisch  : {}",LichtEsstisch.state)  	
      	
    if( ((Sun_Elevation.state as DecimalType).intValue < 0) &&
        ((LichtCouchtisch.state as DecimalType).intValue == 0) &&
        ((LichtEsstisch.state as DecimalType).intValue == 0)  &&
        (LichtFlur.state == OFF ) )
    {  if(timerGuteNacht != null) timerGuteNacht.cancel()
       LichtFlur.sendCommand(ON)
       timerGuteNacht = createTimer(now.plusMinutes(3)) [|
          LichtFlur.sendCommand(OFF)
       timerGuteNacht = null   // reset the timer
    ] 	
    }
  }   
end

rule "LEDStripesAuto"
when
	Item LichtCouchtisch received update or
	Item LichtEsstisch received update
	
then
  logDebug("LEDStripes", "LEDStripesAuto: Entering Rule")
  
  if( LichtButLed.state != null && 
      LichtCouchtisch.state != null &&
      LichtEsstisch.state != null
      ){   
    
    logDebug("LEDStripes", "LEDStripesAuto: LEDStripes in {} mode",LichtButLed.state) 
  
    if(LichtButLed.state == "AUTO"){
 
      val l = (LichtCouchtisch.state as DecimalType).intValue + (LichtEsstisch.state as DecimalType).intValue
    
      logDebug("LEDStripes", "LEDStripesAuto: Couchtisch plus Esstisch is: {} ", l ) 
    
      if(  l > 0 ){
        logDebug("LEDStripes", "LEDStripesAuto: Sending ON") 
        Candle.sendCommand(ON)
        LedCouch.sendCommand(ON)    
        LedTheke.sendCommand(ON)       
        
      } else {
        logDebug("LEDStripes", "LEDStripesAuto: Sending OFF")  
        Candle.sendCommand(OFF)
        LedCouch.sendCommand(OFF)    
        LedTheke.sendCommand(OFF)   
      }
    } 
  }   
end


rule "LEDStripesSet"
when
  Item LichtButLed received command 
then
   logDebug("LEDStripes", "LEDStripesSet: Entering Rule LEDStripesSet")
   if( receivedCommand.toString == "ON" || receivedCommand.toString == "OFF"){
     Candle.sendCommand(receivedCommand.toString)
     LedCouch.sendCommand(receivedCommand.toString)    
     LedTheke.sendCommand(receivedCommand.toString)     
   }
end

rule "LEDStripesInit"
when
   System started
then
  logDebug("LEDStripes", "LEDStripesInit: Entering Rule")
//  postUpdate(LichtButLed,"AUTO")
  LichtButLed.sendCommand("AUTO")
end